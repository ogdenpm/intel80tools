cpmio: do;
mon1:	procedure(f, a) external; declare f byte, a address; end;
mon2:	procedure(f, a) byte external; declare f byte, a address; end;

declare lit literally 'literally',
	CREAD	lit	'1',
	CWRITE	lit	'2',
	LWRITE	lit	'5',
	CWRITESTR lit	'9',
	CREADSTR lit	'10',
	CONSTAT	lit	'11',
	FOPEN	lit	'15',
	FCLOSE	lit	'16',
	FDELETE	lit	'19',
	FREAD	lit	'20',
	FWRITE	lit	'21',
	FMAKE	lit	'22',
	FDMAOFF	lit	'26';

declare	CR	lit	'0dh',
	LF	lit	'0ah',
	DC1	lit	'11h',
	US	lit	'1fh',
	ESC	lit	'1bh';

rdcon: procedure byte public;
	return mon2(CREAD, 0);
end;

wrcon: procedure(c) public;
	declare c byte;
	call mon1(CWRITE, c);
end;

wrconCRLF: procedure public;
	call wrcon(CR);
	call wrcon(LF);
end;

wrlst: procedure(c) public;
	declare c byte;
	call mon1(LWRITE, c);
end;

wrstr: procedure(str) public;
	declare str address;
	call mon1(CWRITESTR, str);
end;

rdstr: procedure(buf) public;
	declare buf address;
	call mon1(CREADSTR, buf);
end;


cstat: procedure byte public;
	return mon2(CONSTAT, 0);
end;

openFile: procedure(fcb) byte public;
	declare fcb address;
	return mon2(FOPEN, fcb);
end;

closeFile: procedure(fcb) public;
	declare fcb address;
	declare junk byte;
	junk = mon2(FCLOSE, fcb);
end;

deleteFile: procedure(fcb) public;
	declare fcb address;
	call mon1(FDELETE, fcb);
end;

readSeq: procedure(fcb, buf) byte public;
	declare (fcb, buf) address;

	call mon1(FDMAOFF, buf);
	return mon2(FREAD, fcb);
end;
end;
