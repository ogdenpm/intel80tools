$       TITLE   ('INTELLEC MODEL 800 MONITOR, VERSION N1.0, 22 NOVEMBER 2017')

CR      EQU     00DH
LF      EQU     00AH

VER     EQU     10
INT0    EQU     00000001B
	
M1234	EQU	01234H		;4660
TRK0	EQU	03000H		;12288
M6789	EQU	06789H		;26505

MEMTOP  EQU     00004H
;RESTART EQU     0FF0FH
;TTYOUT  EQU     0FD25H
;CRTOUT  EQU     0FD3CH
BEGIN   EQU     0F800H
RESTART EQU     0F82CH
TTYOUT  EQU     0F826H
CRTOUT  EQU     0F829H
;
; SHADOW PROM CODE
;
	ORG	00000H

RESET:	JMP	SH0		;BRANCH AROUND IOBYT AND DATE CODE

IOBYT:	DB	000H            ;CON=TTY, RDR=TTY, PCH=TTY, LST=TTY
	DB	009H, 015H      ;DATE STAMP FOR BOOTSTRAP PROM
;
;   A.  INITIALIZE INTERRUPT SYSTEM.
;       1. PROGRAM INTERRUPT SUBSYSTEM (8259)
;       2. MASK ALL INTERRUPTS BUT TRAP LOGIC
;           
SH0:	DI			;DISABLE INTERRUPTS
	MVI	A,012H		;INITIALIZE COMMAND
	OUT	0FDH
	XRA	A
	XRA	A
	OUT	0FCH
	MVI	A,0FEH
	OUT	0FCH
	MVI	A,000H
	OUT	0F3H
;
;   B.  INITIALIZE RAM.
;       1. COMPUTE SIZE OF RAM MEMORY
;
	LXI	H,0		;INITIAL VALUE
LOOP:	INR	H
	MOV	A,M		;FETCH CONTENTS
	CMA			;INVERT IT
	MOV	M,A		;ATTEMPT TO WRITE INTO MEMORY
	CMP	M		;IS LOCATION READ/WRITE?
	CMA			;INVERT AGAIN
	MOV	M,A		;WRITE DATA BACK
	JZ	LOOP		;YES, CONTINUE
	DCX	H		;LAST ADDRESS IN RAM
	SHLD	MEMTOP		;STORE TOP OF MEMORY
;
;       2. SET UP DEDICATED MEMORY LOCATIONS
;               USER I/O ENTRY POINTS (TOP OF MEMORY)
;               EXIT TEMPLATE
;               USER REGISTERS
;               USER INTERRUPT MASK
;               USER STACK
;               MONITOR STACK
;
	LXI	B,TOS		;MOVE EXIT TEMPLATE TO RAM
	MOV	L,C
	SPHL			;SET STACK
SH1:	LDAX	B
	MOV	M,A
	INR	C		;MOVE BOTH POINTERS
	INR	L
	JNZ	SH1		;END ON PAGE BOUNDARY
	MVI	L,0D1H		;SET UP INITIAL VALUE FOR USER STACK
	MOV	M,H		;LOWER HALF OF STACK POINTER IS KNOWN
	DCR	M		;AND DROP IR ONE
;
;               TRAP TO MONITOR
;
	MVI	A,0C3H
	STA	RESET
	LXI	H,RESTART	;SET UP RST0 FOR BREAKPOINT
	SHLD	RESET+1		;LOGIC
;
;   C.  PROGRAM I/O DEVICES
;       1. USART FOR CRT
;       2. USART FOR TTY
;
	MVI	A,04FH          ;ST1+R2400+CL8	
	OUT	0F7H
	MVI	A,0CEH          ;ST2+R110+CL8
	OUT	0F5H
	MVI	A,027H          ;TXEN+DTR+RXEN+RTS
	OUT	0F7H
	OUT	0F5H
;
;       3. HIGH SPEED READER
;       4. HIGH SPEED PUNCH
;       5. TTY READER
;
	XRA	A
	OUT	0F9H
;
;   D.  LOAD ISIS.T0 IF DISKETTE 0 IS READY
;
	IN	078H		;SAMPLE FDCC STATUS
	RRC
	JNC	SH3
	MVI	A,IOPB
	OUT	079H		;LOW(IOPB)
	XRA	A
	OUT	07AH		;HIGH(IOPB), START DISK I/O
SH2:	IN	078H
	ANI	004H		;WAIT FOR FDCC TO COMPLETE
	JZ	SH2
;
;   E.  DETERMINE COLD START CONSOLE
;
SH3:	LXI	H,IOBYT		;POINT TO IOBYTE
	MOV	D,M		;FETCH INTO D
	IN	0F5H
	ANI	002H
	JZ	SH4		;NOT TTY
	IN	0F4H		;GET CHARACTER FROM TTY
	JMP	SH5
SH4:	INR	D		;IOBYTE = CRT
	IN	0F7H
	ANI	002H
	JZ	SH3		;NOT CRT
	IN	0F6H		;GET CHARACTER FROM CRT
SH5:	ANI	07FH
	CPI	' '
	JNZ	SH3
	MOV	M,D		;REPLACE MODIFIED IOBYTE
	MVI	L,006H
	MOV	M,D		;SET INITIAL I/O CONFIGURATION
;
;   F.  IF DISK IS READY, TRANSFER TO ISIS.T0
;
	IN	078H
	RRC
	JC	TRK0
;
;   G.  TYPE SIGN-ON FOR RAM MONITOR
;
	MVI	L,0B1H		;ADDRESS OF MESSAGE
	MVI	B,015H		;LENGTH OF MESSAGE
SH6:	MOV	C,M             ;GET A CHARACTER
	MOV	A,D		;TEST CONSOLE SELECTION
	RRC
	CNC	TTYOUT		;PRINT ON TTY IF TTY IS CONSOLE
	MOV	A,D
	RRC
	CC	CRTOUT		;PRINT ON CRT IF CRT IS CONSOLE
	INX	H
	DCR	B
	JNZ	SH6
;
;   H.  BRANCH TO MONITOR
;
	JMP	BEGIN		;INTERRUPTS ARE DISABLED
;
;       DISK I/O PARAMETER BLOCK
;
IOPB:	DB	080H            ;IOCW, NO UPDATE BIT SET
        DB      004H            ;I/O INSTRUCTION, READ DISK 0
        DB      26              ;READ 26 SECTORS
        DB      0               ;TRACK 0
        DB      1               ;SECTOR 1
        DW      TRK0            ;LOAD ADDRESS
;
;       MDS MONITOR SIGN-ON MESSAGE
;
VERS:   DB      CR,LF,'MDS BOOT, N'
        DB      VER/10+'0','.',VER MOD 10+'0'
        DB      CR,LF
LVER    EQU     $-VERS
;
; EXIT CODE TEMPLATE, TO BE EXECUTED IN RAM
; THIS CODE IS ORIGINATED SO AS TO BE ALIGNED
; AGAINST THE TOP OF A PAGE
;
        ORG     000C8H
;
TOS:                            ;BASE OF MONITOR WORK STACK
USEP    EQU     TOS-8           ;BASE OF DEFAULT USER WORK STACK
ELOC:   DB      0EEH            ;E REGISTER STORAGE
DLOC:   DB      0DDH            ;D REGISTER 
CLOC:   DB      0CCH            ;C REGISTER 
BLOC:   DB      0BBH            ;B REGISTER 
	DB      0		;UNUSED BYTE
ILOC:   DB      NOT INT0        ;INTERRUPT MASK
FLOC:   DB      0FFH            ;CPU FLAGS
ALOC:   DB      0AAH            ;A REGISTER
        DB      USEP            ;LOW(SP)
SLOC:   DB      0               ;HIGH(SP)
;
EXIT:                           ;MONITOR STACK ORIGIN
	DI			;00D2-F3
	POP	D		;00D3-D1
	POP	B		;00D4-C1
	POP	PSW		;00D5-F1
	OUT	0FCH		;00D6-D3 FC
	POP	PSW		;00D8-F1
	POP	H		;00D9-E1
	SPHL			;00DA-F9
	LXI	H,01234H	;00DB-21 34 12 *
LLOC    EQU     $-2
HLOC    EQU     $-1
	EI			;00DE-FB
	JMP	06789H		;00DF-C3 89 67 *
PLOC    EQU     $-1
TLOC:   DW      0               ;TRAP 1 ADDRESS
        DB      0               ;TRAP 1 VALUE
        DW      0               ;TRAP 2 ADDRESS
        DB      0               ;TRAP 2 VALUE
;
; EXTENSIBLE I/O ENTRY POINTS
;
XTBL:
CILOC:	JMP	0		;00E8-C3 00 00 *
COLOC:	JMP	0		;00EB-C3 00 00 *
R1LOC:  JMP	0		;00EE-C3 00 00 *
R2LOC:	JMP	0		;00F1-C3 00 00 *
P1LOC:	JMP	0		;00F4-C3 00 00 *
P2LOC:	JMP	0		;00F7-C3 00 00 *
L1LOC:	JMP	0		;00FA-C3 00 00 *
L2LOC:	JMP	0		;00FD-C3 00 00 *

	END
