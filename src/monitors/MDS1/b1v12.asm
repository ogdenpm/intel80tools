$       TITLE   ('INTELLEC/MDS BOOTSTRAP, VERSION 1.2, 15 SEPTEMBER 1975')
;
;          INTELLEC/MDS BOOTSTRAP
;              VERSION 1.2
;
;          COPYRIGHT (C) 1974, 1975
;          INTEL CORPORATION
;          3065 BOWERS AVENUE
;          SANTA CLARA, CALIFORNIA 95051
;
;
VER     EQU     12              ;VERSION 1.2
DATE    EQU     1509H           ;CREATION DATE, 15 SEPTEMBER 1975
;
; NOTE:
;
; THE DATE SHOWN ABOVE IS ENCODED IN A TWO BYTE FIELD
; IN BOTH THE BOOTSTRAP PROM AND THE MONITOR ROM IN ORDER
; TO CONTROL NEW RELEASES OF THIS PROGRAM.
;
; IN THE BOOTSTRAP PROM, THE DATE CODE IS LOCATED AT
; ADDRESSES 4 AND 5.
;
; IN THE MONITOR ROM, THE DATE CODE IS LOCATED AT ADDRESSES
; 0F824H AND 0F825H.
;
; IF AND WHEN A NEW RELEASE IS ISSUED, PLEASE CHANGE 
; THE DATE CODE.
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
; INTELLEC/MDS SYSTEM CONSTANTS
;
;
; TTY AND CRT STATUS BITS
;
TRDY    EQU     00000001B       ;TRANSMIT READY
RBR     EQU     00000010B       ;RECEIVE BUFFER READY
TBE     EQU     00000100B       ;TRANSMIT BUFFER EMPTY
RPAR    EQU     00001000B       ;RECEIVE PARITY ERROR
ROV     EQU     00010000B       ;RECEIVE OVERRUN ERROR
RFR     EQU     00100000B       ;RECEIVE FRAMING ERROR 
DSR     EQU     10000000B       ;DATA SET READY INPUT
;
; TTY AND CRT INITIALIZATION CONTROLS
;
R48@1   EQU     00000010B       ;4800 BAUD @ JUMPER 1
R96@1   EQU     00000001B       ;9600 BAUD @ JUMPER 1
R24@1   EQU     00000011B       ;2400 BAUD @ JUMPER 1
R6@2    EQU     00000010B       ;600 BAUD @ JUMPER 2
R12@2   EQU     00000001B       ;1200 BAUD @ JUMPER 2
R3@2    EQU     00000011B       ;300 BAUD @ JUMPER 2
R110    EQU     00000010B       ;110 BAUD @ JUMPER 2
CL8     EQU     00001100B       ;CHARACTER LENGTH = 8
CL7     EQU     00001000B       ;CHARACTER LENGTH = 7
CL6     EQU     00000100B       ;CHARACTER LENGTH = 6
CL5     EQU     00000000B       ;CHARACTER LENGTH = 5
ST1     EQU     01000000B       ;1 STOP BIT
ST15    EQU     10000000B       ;1.5 STOP BITS
ST2     EQU     11000000B       ;2 STOP BITS
PENB    EQU     00010000B       ;PARITY ENABLE
PEVEN   EQU     00100000B       ;EVEN PARITY
TXEN    EQU     00000001B       ;TRANSMITTER ENABLE
DTR     EQU     00000010B       ;DATA TERMINAL READY
RXEN    EQU     00000100B       ;ENABLE RECEIVER
CLERR   EQU     00010000B       ;CLEAR ERROR
USRST   EQU     01000000B       ;USART INIT - RETURN TO MODE CONTROL CYCLE
RTS     EQU     00100000B       ;SET REQUEST TO SEND OUTPUT 
;
; PTR, PTP, AND TTY READER CONTROLS
;
PTPREV  EQU     00010000B       ;PUNCH REVERSE DIRECTION
PTPADV  EQU     00100000B       ;PUNCH ADVANCE
PTRREV  EQU     00000100B       ;READER REVERSE DIRECTION
PTRADV  EQU     00001000B       ;READER ADVANCE
TTYADV  EQU     00000100B       ;TTY ADVANCE
;
; LPT, PTR AND PTP STATUS BITS
;
LPTRY   EQU    00000001B        ;LPT READY
PTRDY   EQU    00000001B        ;PTR READY WITH DATA
PTPRY   EQU    00000100B        ;PTP READY FOR DATA
;
; TTY I/O CONSTANTS
;
TTI     EQU     0F4H            ;TTY INPUT DATA PORT
TTO     EQU     0F4H            ;TTY OUTPUT DATA PORT
TTS     EQU     0F5H            ;TTY INPUT STATUS PORT
TTC     EQU     0F5H            ;TTY OUTPUT CONTROL PORT
;
; CRT I/O CONSTANTS
;
CRTI    EQU     0F6H            ;CRT INPUT DATA PORT 
CRTS    EQU     0F7H            ;CRT INPUT STATUS PORT
CRTO    EQU     0F6H            ;CRT OUTPUT DATA PORT     
CRTC    EQU     0F7H            ;CRT OUTPUT CONTROL PORT
;
; PTR I/O CONSTANTS
;
PTRI    EQU     0F8H            ;PTR INPUT DATA PORT
PTRS    EQU     0F9H            ;PTR INPUT STATUS PORT
PTRC    EQU     0F9H            ;PTR OUTPUT COMMAND PORT
;
; PTP I/O CONSTANTS
;
PTPO    EQU     0F8H            ;PTP INPUT DATA PORT
PTPS    EQU     0F9H            ;PTP INPUT STATUS PORT
PTPC    EQU     0F9H            ;PTP OUTPUT COMMAND PORT
;
; LPT I/O CONSTANTS
;
LPTO    EQU     0F8H            ;LPT INPUT DATA PORT
LPTS    EQU     0F9H            ;LPT INPUT STATUS PORT
LPTC    EQU     0F9H            ;LPT OUTPUT COMMAND PORT
;
; REAL TIME CLOCK CONSTANTS
; EACH TICK = 1.0 MS
;
RTC     EQU     0FFH            ;REAL TIME CLOCK PORT
RTCS    EQU     00000001B       ;REAL TIME CLOCK STATUS
BOOT    EQU     00000010B       ;BOOTSTRAP MODE INDICATOR, 1 = ON
;
; PROGRAMMER I/O CONSTANTS
;
PHI     EQU     0F1H            ;PROM COMMAND AND MSB ADDRESS BITS
PLO     EQU     0F2H            ;PROM ADDRESS BITS (8 LSB)
PDATA   EQU     0F0H            ;PROM DATA PORT
PSTAT   EQU     0F1H            ;PROM STATUS PORT
PCOMP   EQU     00000010B       ;PROGRAMMING COMPLETE
PGRDY   EQU     00000001B       ;PROM READY
PSOCK   EQU     00100000B       ;16 PIN SOCKET SELECTED
PNIB    EQU     00010000B       ;SELECT UPPER NIBBLE
;
; FDCC CONSTANTS
;
LOWW    EQU     79H             ;LOW(IOPB)
HI      EQU     7AH             ;HIGH(IOPB)
DSTAT   EQU     78H             ;DISK STATUS
TRK0    EQU     3000H           ;FIRST ADDRESS OF DISK BOOTSTRAP
;
; CONDITIONAL ASSEMBLY SWITCHES
;
FALSE   EQU     0
TRUE    EQU     NOT FALSE
DEBUG   EQU     FALSE
;
; GLOBAL CONSTANTS
;
TOUT    EQU     250             ;250 MS. COUNTER FOR READER TIMEOUT
CR      EQU     0DH
LF      EQU     0AH
ETX     EQU     03H
;
; I/O STATUS BYTE MASKS AND VALUES
;
CMSK    EQU     11111100B       ;MASK FOR CONSOLE I/O
RMSK    EQU     11110011B       ;MASK FOR READER INPUT
PMSK    EQU     11001111B       ;MASK FOR PUNCH OUTPUT
LMSK    EQU     00111111B       ;MASK FOR LIST OUTPUT
;
CTTY    EQU     00000000B       ;CONSOLE I/O = TTY
CCRT    EQU     00000001B       ;CONSOLE I/O = CRT
BATCH   EQU     00000010B       ;BATCH MODE,
                                ;INPUT = READER, OUTPUT = LIST
CUSE    EQU     00000011B       ;USER DEFINED CONSOLE I/O
RTTY    EQU     00000000B       ;READER = TTY
RPTR    EQU     00000100B       ;READER = PTR
RUSE1   EQU     00001000B       ;USER DEFINED READER (1)
RUSE2   EQU     00001100B       ;USER DEFINED READER (2)
PTTY    EQU     00000000B       ;PUNCH = TTY
PPTP    EQU     00010000B       ;PUNCH = PTP
PUSE1   EQU     00100000B       ;USER DEFINED PUNCH (1)
PUSE2   EQU     00110000B       ;USER DEFINED PUNCH (2)
LTTY    EQU     00000000B       ;LIST = TTY
LCRT    EQU     01000000B       ;LIST = CRT
LLPT    EQU     10000000B       ;LIST = LPT
LUSE    EQU     11000000B       ;USER DEFINED LIST
;
; INTERRUPT SYSTEM MASKS AND VALUES
;
INT0    EQU     00000001B       ;MASK FOR INTERRUPT LEVEL 0
INT1    EQU     00000010B
INT2    EQU     00000100B
INT3    EQU     00001000B
INT4    EQU     00010000B
INT5    EQU     00100000B
INT6    EQU     01000000B
INT7    EQU     10000000B
;
MASK    EQU     0FCH            ;MASK PORT
REVRT   EQU     0FDH            ;INTERRUPT REVERT
LOCK    EQU     0FEH            ;BUS OVERRIDE
ISTAT   EQU     0FAH            ;INTERRUPT STATUS PORT
ICON    EQU     0F3H            ;INTERRUPT CONTROL PORT
EOI     EQU     00100000B       ;END OF INTERRUPT
;
; INTERRUPT STATUS AND CONTROL BITS
;
ITTYO   EQU     00000001B
ITTYI   EQU     00000010B
IPTP    EQU     00000100B
IPTR    EQU     00001000B
ICRTO   EQU     00010000B
ICRTI   EQU     00100000B
ILPT    EQU     01000000B
MENB    EQU     10000000B
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
; PAGE 0 DEDICATED RAM LOCATIONS, INITIALIZED BY BOOTSTRAP PROM CODE.
;
        ORG     0
INIT:  DS      3               ; TRAP TO MONITOR RESTART
IOBYT:  DS      1               ; I/O SYSTEM STATUS BYTE
MSK:
MEMTOP: DS      2               ; TOP OF RAM, ONLY H SAVED
INITIO: DS      1               ; INITIAL I/O CONFIGURATION
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
; MACRO DEFINITIONS
;
        IF      0
FETCH:  MACRO   VALUE           ;FETCH AN ADDRESS IN THE STACK
        LXI     H,VALUE
        DAD     SP
        ENDM
;
GET     MACRO   VALUE           ;COMPUTE RAM ADDRESS BASED ON STACK
        LHLD    MEMTOP
        MVI     L,VALUE AND 0FFH
        ENDM
;
WHILE   MACRO   CHAR            ;SCAN INPUT WHILE EQUAL
LOOP:   
        CALL    TI
        CPI     CHAR
        JZ      LOOP
        ENDM
;
UNTIL   MACRO   CHAR            ;SCAN INPUT UNTIL EQUAL
LOOP:   
        CALL    TI
        CPI     CHAR
        JNZ     LOOP
        ENDM
;
SIZE    MACRO                   ;FIND TOP OF MEMORY
        LXI     H,0             ;INITIAL VALUE
LOOP:
        INR     H
        MOV     A,M             ;FETCH CONTENTS OF MEMORY
        CMA                     ;INVERT IT
        MOV     M,A             ;ATTEMPT TO WRITE INTO MEMORY
        CMP     M               ;IS LOCATION READ/WRITE?
        CMA                     ;INVERT AGAIN
        MOV     M,A             ;WRITE DATA BACK
        JZ      LOOP            ;YES, CONTINUE
        DCX     H               ;LAST ADDRESS IN RAM
        SHLD    MEMTOP          ;STORE TOP OF MEMORY
        ENDM
;
; CASE BRANCH MACRO
; INPUT PARAMETERS
; REGISTER A - CASE INDEX, 0...N
; PARAMETER 1 - ADDRESS OF BRANCH TABLE
; PARAMETER 2 - LENGTH OF BRANCH TABLE
; USES REGISTERS A,D,E,H,L
;
CASE    MACRO   TABLE, RANGE
        LXI     H,TABLE
        CPI     RANGE           ;TEST FOR OVERRUN
        JP      ERROR
        MOV     E,A             ;MOVE INDEX TO DE
        MVI     D,0
        DAD     D               ;ADD BASE + 2 * INDEX -> HL
        DAD     D
        MOV     A,M             ;GET LSB OF BRANCH LOCATION
        INX     H
        MOV     H,M             ;GET MSB OF BRANCH ADDRESS
        MOV     L,A
        PCHL
        ENDM
        ENDIF
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
;       ADDRESS CONSTANTS FOR VERSION 1.2 ONLY
;       ======= ========= === ======= === ====
;
;       THE FOLLOWING FOUR ADDRESSES ARE INSERTED FOR ASSEMBLING
;       THE VERSION 1.2 BOOTSTRAP PROM CODE ONLY. THEY SPECIFY THE
;       ADDRESSES OF THE THREE ROUTINES IN THE MONITOR ROM
;       WHICH ARE CALLED FROM THE BOOTSTRAP PROM. THESE ARE THE
;       ADDRESSES OF THESE ROUTINES IN THE VERSION 1.1 MONITOR ROM
;
;       MODIFIED WITH JMPS IN THE MONITOR CODE AT START
;
RESTART EQU     0F847H
TTYOUT  EQU     0F84AH
CRTOUT  EQU     0F84DH
BEGIN   EQU     0F800H
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
; SHADOW PROM CODE
;
SBASE   SET     0
        ORG     SBASE
;
;   A.  INITIALIZE INTERRUPT SYSTEM.
;       1. PROGRAM INTERRUPT SUBSYSTEM (8259)
;       2. MASK ALL INTERRUPTS BUT TRAP LOGIC
;           
	JMP	SH0		;BRANCH AROUND IOBYT AND DATE CODE
RESET:	DB	000H            ;CON=TTY, RDR=TTY, PCH=TTY, LST=TTY
	DW	DATE            ;DATE STAMP FOR BOOTSTRAP PROM
SH0:	DI			;DISABLE INTERRUPTS
	MVI	A,012H		;INITIALIZE COMMAND
	OUT	REVRT
	XRA	A
	XRA	A
	OUT	MASK
	MVI	A,NOT INT0
	OUT	MASK
	MVI	A,0
	OUT	ICON
;
;   B.  INITIALIZE RAM.
;       1. COMPUTE SIZE OF RAM MEMORY
;
	LXI	H,0		;INITIAL VALUE
LOOP:	INR	H
	MOV	A,M		;FETCH CONTENTS
	CMA			;INVERT IT
	MOV	M,A		;ATTEMPT TO WRITE INTO MEMORY
	CMP	M		;IS LOCATION READ/WRITE?
	CMA			;INVERT AGAIN
	MOV	M,A		;WRITE DATA BACK
	JZ	LOOP		;YES, CONTINUE
	DCX	H		;LAST ADDRESS IN RAM
	SHLD	MEMTOP		;STORE TOP OF MEMORY
;
;       2. SET UP DEDICATED MEMORY LOCATIONS
;               USER I/O ENTRY POINTS (TOP OF MEMORY)
;               EXIT TEMPLATE
;               USER REGISTERS
;               USER INTERRUPT MASK
;               USER STACK
;               MONITOR STACK
;
	LXI	B,TOS		;MOVE EXIT TEMPLATE TO RAM
	MOV	L,C
	SPHL			;SET STACK
SH1:	LDAX	B
	MOV	M,A
	INR	C		;MOVE BOTH POINTERS
	INR	L
	JNZ	SH1		;END ON PAGE BOUNDARY
	MVI	L,SLOC		;SET UP INITIAL VALUE FOR USER STACK
	MOV	M,H		;LOWER HALF OF STACK POINTER IS KNOWN
	DCR	M		;AND DROP IR ONE
;
;               TRAP TO MONITOR (0-2)
;
	MVI	A,(JMP RESTART)
	STA	RESET
	LXI	H,RESTART	;SET UP RST0 FOR BREAKPOINT
	SHLD	RESET+1		;LOGIC
;
;   C.  PROGRAM I/O DEVICES
;       1. USART FOR CRT
;       2. USART FOR TTY
;
	MVI	A,ST1 OR R24@1 OR CL8 ;04FH
	OUT	CRTC
	MVI	A,ST2 OR R110 OR CL8  ;0CEH
	OUT	TTC
	MVI	A,TXEN OR DTR OR RXEN OR RTS ;027H
	OUT	CRTC
	OUT	TTC
;
;       3. HIGH SPEED READER
;       4. HIGH SPEED PUNCH
;       5. TTY READER
;
	XRA	A
	OUT	PTRC
;
;   D.  LOAD ISIS.T0 IF DISKETTE 0 IS READY
;
	IN	DSTAT		;SAMPLE FDCC STATUS
	RRC
	JNC	SH3
	MVI	A,IOPB
	OUT	LOWW		;LOW(IOPB)
	XRA	A
	OUT	HI		;HIGH(IOPB), START DISK I/O
SH2:	IN	DSTAT
	ANI	004H		;WAIT FOR FDCC TO COMPLETE
	JZ	SH2
;
;   E.  DETERMINE COLD START CONSOLE
;
SH3:	LXI	H,IOBYT		;POINT TO IOBYTE
	MOV	D,M		;FETCH INTO D
	IN	TTS
	ANI	RBR
	JZ	SH4		;NOT TTY
	IN	TTI		;GET CHARACTER FROM TTY
	JMP	SH5
SH4:	INR	D		;IOBYTE = CRT
	IN	CRTS
	ANI	RBR
	JZ	SH3		;NOT CRT
	IN	CRTI		;GET CHARACTER FROM CRT
SH5:	ANI	07FH
	CPI	' '
	JNZ	SH3
	MOV	M,D		;REPLACE MODIFIED IOBYTE
	MVI	L,INITIO
	MOV	M,D		;SET INITIAL I/O CONFIGURATION
;
;   F.  IF DISK IS READY, TRANSFER TO ISIS.T0
;
	IN	DSTAT
	RRC
	JC	TRK0
;
;   G.  TYPE SIGN-ON FOR ROM MONITOR
;
	MVI	L,VERS		;ADDRESS OF MESSAGE
	MVI	B,LVER		;LENGTH OF MESSAGE
SH6:	MOV	C,M             ;GET A CHARACTER
	MOV	A,D		;TEST CONSOLE SELECTION
	RRC
	CNC	TTYOUT		;PRINT ON TTY IF TTY IS CONSOLE
	MOV	A,D
	RRC
	CC	CRTOUT		;PRINT ON CRT IF CRT IS CONSOLE
	INX	H
	DCR	B
	JNZ	SH6
;
;   H.  BRANCH TO MONITOR
;
	JMP	BEGIN		;INTERRUPTS ARE DISABLED
;
;       DISK I/O PARAMETER BLOCK
;
IOPB:	DB	080H            ;IOCW, NO UPDATE BIT SET
        DB      004H            ;I/O INSTRUCTION, READ DISK 0
        DB      26              ;READ 26 SECTORS
        DB      0               ;TRACK 0
        DB      1               ;SECTOR 1
        DW      TRK0            ;LOAD ADDRESS
;
;       MDS MONITOR SIGN-ON MESSAGE
;
VERS:   DB      CR,LF,'MDS BOOT, V'
        DB      VER/10+'0','.',VER MOD 10+'0'
        DB      CR,LF
LVER    EQU     $-VERS
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-•-*-*-*-*-*-*-*-*-*-*-*-•-*-*-*-*-*
;
; EXIT CODE TEMPLATE, TO BE EXECUTED IN RAM
; THIS CODE IS ORIGINATED SO AS TO BE ALIGNED
; AGAINST THE TOP OF A PAGE
;
        ORG     SBASE+0C8H
;
TOS:                            ;BASE OF MONITOR WORK STACK
USEP    EQU     TOS-8           ;BASE OF DEFAULT USER WORK STACK
ELOC:   DB      0EEH            ;E REGISTER STORAGE
DLOC:   DB      0DDH            ;D REGISTER 
CLOC:   DB      0CCH            ;C REGISTER 
BLOC:   DB      0BBH            ;B REGISTER 
	DB      0		;UNUSED BYTE
ILOC:   DB      NOT INT0        ;INTERRUPT MASK
FLOC:   DB      0FFH            ;CPU FLAGS
ALOC:   DB      0AAH            ;A REGISTER
        DB      USEP            ;LOW(SP)
SLOC:   DB      0               ;HIGH(SP)
;
EXIT:                           ;MONITOR STACK ORIGIN
	DI			;RESTART SEQUENCE
	POP	D
	POP	B
	POP	PSW
	OUT	MASK
	POP	PSW
	POP	H
	SPHL
	LXI	H,01234H
LLOC    EQU     $-2
HLOC    EQU     $-1
	EI
	JMP	06789H
PLOC    EQU     $-1
TLOC:   DW      0               ;TRAP 1 ADDRESS
        DB      0               ;TRAP 1 VALUE
        DW      0               ;TRAP 2 ADDRESS
        DB      0               ;TRAP 2 VALUE
;
; EXTENSIBLE I/O ENTRY POINTS
;
XTBL:
CILOC:	JMP	0
COLOC:	JMP	0
R1LOC:  JMP	0
R2LOC:	JMP	0
P1LOC:	JMP	0
P2LOC:	JMP	0
L1LOC:	JMP	0
CSLOC:	JMP	0
ENDX    EQU     $               ;THIS LABEL SHOULD BE AT 100H
;
; END OF SHADOW PROM CODE
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*

	END
