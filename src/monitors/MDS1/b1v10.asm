$       TITLE   ('INTELLEC/MDS BOOTSTRAP, VERSION 1.0, 1974')
;
;         INTELLEC/MDS BOOTSTRAP
;             VERSION 1.0
;
;         COPYRIGHT (C) 1974
;         INTEL CORPORATION
;         3065 BOWERS AVENUE
;         SANTA CLARA, CALIFORNIA 95051
;
;
VER     EQU     10              ;VERSION 1.0
DATE    EQU     1004H           ;CREATION DATE, 1974
;
; NOTE:
;
; THE DATE SHOWN ABOVE IS ENCODED IN A TWO BYTE FIELD
; IN BOTH THE BOOTSTRAP PROM AND THE MONITOR ROM IN ORDER
; TO CONTROL NEW RELEASES OF THIS PROGRAM.
;
; IN THE BOOTSTRAP PROM, THE DATE CODE IS LOCATED AT
; ADDRESSES 4 AND 5.
;
; IN THE MONITOR ROM, THE DATE CODE IS LOCATED AT ADDRESSES
; 0F824H AND 0F825H.
;
; IF AND WHEN A NEW RELEASE IS ISSUED, PLEASE CHANGE 
; THE DATE CODE.
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
; INTELLEC/MDS SYSTEM CONSTANTS
;
;
; TTY AND CRT STATUS BITS
;
TRDY    EQU     00000001B       ;TRANSMIT READY
RBR     EQU     00000010B       ;RECEIVE BUFFER READY
TBE     EQU     00000100B       ;TRANSMIT BUFFER EMPTY
RPAR    EQU     00001000B       ;RECEIVE PARITY ERROR
ROV     EQU     00010000B       ;RECEIVE OVERRUN ERROR
RFR     EQU     00100000B       ;RECEIVE FRAMING ERROR 
DSR     EQU     10000000B       ;DATA SET READY INPUT
;
; TTY AND CRT INITIALIZATION CONTROLS
;
R48@1   EQU     00000010B       ;4800 BAUD @ JUMPER 1
R96@1   EQU     00000001B       ;9600 BAUD @ JUMPER 1
R24@1   EQU     00000011B       ;2400 BAUD @ JUMPER 1
R6@2    EQU     00000010B       ;600 BAUD @ JUMPER 2
R12@2   EQU     00000001B       ;1200 BAUD @ JUMPER 2
R3@2    EQU     00000011B       ;300 BAUD @ JUMPER 2
R110    EQU     00000010B       ;110 BAUD @ JUMPER 2
CL8     EQU     00001100B       ;CHARACTER LENGTH = 8
CL7     EQU     00001000B       ;CHARACTER LENGTH = 7
CL6     EQU     00000100B       ;CHARACTER LENGTH = 6
CL5     EQU     00000000B       ;CHARACTER LENGTH = 5
ST1     EQU     01000000B       ;1 STOP BIT
ST15    EQU     10000000B       ;1.5 STOP BITS
ST2     EQU     11000000B       ;2 STOP BITS
PENB    EQU     00010000B       ;PARITY ENABLE
PEVEN   EQU     00100000B       ;EVEN PARITY
TXEN    EQU     00000001B       ;TRANSMITTER ENABLE
DTR     EQU     00000010B       ;DATA TERMINAL READY
RXEN    EQU     00000100B       ;ENABLE RECEIVER
CLERR   EQU     00010000B       ;CLEAR ERROR
USRST   EQU     01000000B       ;USART INIT - RETURN TO MODE CONTROL CYCLE
RTS     EQU     00100000B       ;SET REQUEST TO SEND OUTPUT 
;
; PTR, PTP, AND TTY READER CONTROLS
;
PTPREV  EQU     00010000B       ;PUNCH REVERSE DIRECTION
PTPADV  EQU     00100000B       ;PUNCH ADVANCE
PTRREV  EQU     00000100B       ;READER REVERSE DIRECTION
PTRADV  EQU     00001000B       ;READER ADVANCE
TTYADV  EQU     00000100B       ;TTY ADVANCE
;
; LPT, PTR AND PTP STATUS BITS
;
LPTRY   EQU    00000001B        ;LPT READY
PTRDY   EQU    00000001B        ;PTR READY WITH DATA
PTPRY   EQU    00000100B        ;PTP READY FOR DATA
;
; TTY I/O CONSTANTS
;
TTI     EQU     0F4H            ;TTY INPUT DATA PORT
TTO     EQU     0F4H            ;TTY OUTPUT DATA PORT
TTS     EQU     0F5H            ;TTY INPUT STATUS PORT
TTC     EQU     0F5H            ;TTY OUTPUT CONTROL PORT
;
; CRT I/O CONSTANTS
;
CRTI    EQU     0F6H            ;CRT INPUT DATA PORT 
CRTS    EQU     0F7H            ;CRT INPUT STATUS PORT
CRTO    EQU     0F6H            ;CRT OUTPUT DATA PORT     
CRTC    EQU     0F7H            ;CRT OUTPUT CONTROL PORT
;
; PTR I/O CONSTANTS
;
PTRI    EQU     0F8H            ;PTR INPUT DATA PORT
PTRS    EQU     0F9H            ;PTR INPUT STATUS PORT
PTRC    EQU     0F9H            ;PTR OUTPUT COMMAND PORT
;
; PTP I/O CONSTANTS
;
PTPO    EQU     0F8H            ;PTP INPUT DATA PORT
PTPS    EQU     0F9H            ;PTP INPUT STATUS PORT
PTPC    EQU     0F9H            ;PTP OUTPUT COMMAND PORT
;
; LPT I/O CONSTANTS
;
LPTO    EQU     0F8H            ;LPT INPUT DATA PORT
LPTS    EQU     0F9H            ;LPT INPUT STATUS PORT
LPTC    EQU     0F9H            ;LPT OUTPUT COMMAND PORT
;
; REAL TIME CLOCK CONSTANTS
; EACH TICK = 1.0 MS
;
RTC     EQU     0FFH            ;REAL TIME CLOCK PORT
RTCS    EQU     00000001B       ;REAL TIME CLOCK STATUS
BOOT    EQU     00000010B       ;BOOTSTRAP MODE INDICATOR, 1 = ON
;
; PROGRAMMER I/O CONSTANTS
;
PHI     EQU     0F1H            ;PROM COMMAND AND MSB ADDRESS BITS
PLO     EQU     0F2H            ;PROM ADDRESS BITS (8 LSB)
PDATA   EQU     0F0H            ;PROM DATA PORT
PSTAT   EQU     0F1H            ;PROM STATUS PORT
PCOMP   EQU     00000010B       ;PROGRAMMING COMPLETE
PGRDY   EQU     00000001B       ;PROM READY
PSOCK   EQU     00100000B       ;16 PIN SOCKET SELECTED
PNIB    EQU     00010000B       ;SELECT UPPER NIBBLE
;
; FDCC CONSTANTS
;
LOWW    EQU     79H             ;LOWW(IOPB)
HI      EQU     7AH             ;HIGH(IOPB)
DSTAT   EQU     78H             ;DISK STATUS
TRK0    EQU     3000H           ;FIRST ADDRESS OF DISK BOOTSTRAP
;
; CONDITIONAL ASSEMBLY SWITCHES
;
FALSE   EQU     0
TRUE    EQU     NOT FALSE
DEBUG   EQU     FALSE
;
; GLOBAL CONSTANTS
;
TOUT    EQU     250             ;250 MS. COUNTER FOR READER TIMEOUT
CR      EQU     0DH
LF      EQU     0AH
ETX     EQU     03H
;
; I/O STATUS BYTE MASKS AND VALUES
;
CMSK    EQU     11111100B       ;MASK FOR CONSOLE I/O
RMSK    EQU     11110011B       ;MASK FOR READER INPUT
PMSK    EQU     11001111B       ;MASK FOR PUNCH OUTPUT
LMSK    EQU     00111111B       ;MASK FOR LIST OUTPUT
;
CTTY    EQU     00000000B       ;CONSOLE I/O = TTY
CCRT    EQU     00000001B       ;CONSOLE I/O = CRT
BATCH   EQU     00000010B       ;BATCH MODE,
                                ;INPUT = READER, OUTPUT = LIST
CUSE    EQU     00000011B       ;USER DEFINED CONSOLE I/O
RTTY    EQU     00000000B       ;READER = TTY
RPTR    EQU     00000100B       ;READER = PTR
RUSE1   EQU     00001000B       ;USER DEFINED READER (1)
RUSE2   EQU     00001100B       ;USER DEFINED READER (2)
PTTY    EQU     00000000B       ;PUNCH = TTY
PPTP    EQU     00010000B       ;PUNCH = PTP
PUSE1   EQU     00100000B       ;USER DEFINED PUNCH (1)
PUSE2   EQU     00110000B       ;USER DEFINED PUNCH (2)
LTTY    EQU     00000000B       ;LIST = TTY
LCRT    EQU     01000000B       ;LIST = CRT
LLPT    EQU     10000000B       ;LIST = LPT
LUSE    EQU     11000000B       ;USER DEFINED LIST
;
; INTERRUPT SYSTEM MASKS AND VALUES
;
INT0    EQU     00000001B       ;MASK FOR INTERRUPT LEVEL 0
INT1    EQU     00000010B
INT2    EQU     00000100B
INT3    EQU     00001000B
INT4    EQU     00010000B
INT5    EQU     00100000B
INT6    EQU     01000000B
INT7    EQU     10000000B
;
MASK    EQU     0FCH            ;MASK PORT
REVRT   EQU     0FDH            ;INTERRUPT REVERT
LOCK    EQU     0FEH            ;BUS OVERRIDE
ISTAT   EQU     0FAH            ;INTERRUPT STATUS PORT
ICON    EQU     0F3H            ;INTERRUPT CONTROL PORT
EOI     EQU     00100000B       ;END OF INTERRUPT
;
; INTERRUPT STATUS AND CONTROL BITS
;
ITTYO   EQU     00000001B
ITTYI   EQU     00000010B
IPTP    EQU     00000100B
IPTR    EQU     00001000B
ICRTO   EQU     00010000B
ICRTI   EQU     00100000B
ILPT    EQU     01000000B
MENB    EQU     10000000B
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
; PAGE 0 DEDICATED RAM LOCATIONS, INITIALIZED BY BOOTSTRAP PROM CODE.
;
        ORG     0
RESET:
        DS      3               ;TRAP TO MONITOR RESTART
IOBYT:
        DS      1               ;I/O SYSTEM STATUS BYTE
MSK:
MEMTOP:
        DS      2               ;TOP OF RAM, ONLY H SAVED
INITIO:
        DS      1               ;INITIAL I/O CONFIGURATION
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
; MACRO DEFINITIONS
;
        IF      0        
FETCH:  MACRO   VALUE           ;FETCH AN ADDRESS IN THE STACK
        LXI     H,VALUE
        DAD     SP
        ENDM
;
GET     MACRO   VALUE           ;COMPUTE RAM ADDRESS BASED ON STACK
        LHLD    MEMTOP
        MVI     L,VALUE AND 0FFH
        ENDM
;
WHILE   MACRO   CHAR            ;SCAN INPUT WHILE EQUAL
LOOP:   
        CALL    TI
        CPI     CHAR
        JZ      LOOP
        ENDM
;
UNTIL   MACRO   CHAR            ;SCAN INPUT UNTIL EQUAL
LOOP:   
        CALL    TI
        CPI     CHAR
        JNZ     LOOP
        ENDM
;
SIZE    MACRO                   ;FIND TOP OF MEMORY
        LXI     H,0             ;INITIAL VALUE
LOOP:
        INR     H
        MOV     A,M             ;FETCH CONTENTS OF MEMORY
        CMA                     ;INVERT IT
        MOV     M,A             ;ATTEMPT TO WRITE INTO MEMORY
        CMP     M               ;IS LOCATION READ/WRITE?
        CMA                     ;INVERT AGAIN
        MOV     M,A             ;WRITE DATA BACK
        JZ      LOOP            ;YES, CONTINUE
        DCX     H               ;LAST ADDRESS IN RAM
        SHLD    MEMTOP          ;STORE TOP OF MEMORY
        ENDM
;
;CASE BRANCH MACRO
;INPUT PARAMETERS
;REGISTER A - CASE INDEX, 0...N
;PARAMETER 1 - ADDRESS OF BRANCH TABLE
;PARAMETER 2 - LENGTH OF BRANCH TABLE
;USES REGISTERS A,D,E,H,L
;
CASE    MACRO   TABLE, RANGE
        LXI     H,TABLE
        CPI     RANGE           ;TEST FOR OVERRUN
        JP      ERROR
        MOV     E,A             ;MOVE INDEX TO DE
        MVI     D,0
        DAD     D               ;ADD BASE + 2 * INDEX -> HL
        DAD     D
        MOV     A,M             ;GET LSB OF BRANCH LOCATION
        INX     H
        MOV     H,M             ;GET MSB OF BRANCH ADDRESS
        MOV     L,A
        PCHL
        ENDM
        ENDIF
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
;      ADDRESS CONSTANTS FOR VERSION 1.0 ONLY
;      ======= ========= === ======= === ====
;
;      THE FOLLOWING FOUR ADDRESSES ARE INSERTED FOR ASSEMBLING
;      THE VERSION 1.2 BOOTSTRAP PROM CODE ONLY. THEY SPECIFY THE
;      ADDRESSES OF THE THREE ROUTINES IN THE MONITOR ROM
;      WHICH ARE CALLED FROM THE BOOTSTRAP PROM. THESE ARE THE
;      ADDRESSES OF THESE ROUTINES IN THE VERSION 1.1 MONITOR ROM
;
RESTART EQU     0F847H
TTYOUT  EQU     0F84AH
CRTOUT  EQU     0F84DH
BEGIN   EQU     0F800H
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
;
; SHADOW PROM CODE
;
SBASE   SET     0
        ORG     SBASE
;
;  A.  INITIALIZE INTERRUPT SYSTEM.
;      1. PROGRAM INTERRUPT SUBSYSTEM (8259)
;      2. MASK ALL INTERRUPTS BUT TRAP LOGIC
;          
	JMP	SH0	;0000-C3 06 00 *
INIT:	DB	000H
        DW	DATE	;ROM DATE STAMP APRIL 10
SH0:
	MVI	A,012H	;0007-3E 12
	OUT	REVRT	;REVERT INTERRUPT SYSTEM 0009-D3 FD
	MVI	A,0
	OUT	MASK	;MASK PORT 000D-D3 FC
	MVI	A,0FEH	;000F-3E FE
	OUT	MASK	;MASK PORT 0011-D3 FC
	MVI	A,000H	;0013-3E 00
	OUT	ICON	;INTERRUPT CONTROL PORT 0015-D3 F3
;
;  B.  INITIALIZE RAM.
;      1. COMPUTE SIZE OF RAM MEMORY
;
	LXI	H,0	;0017-21 00 00 *
LOOP:
	INR	H	;INR PAGE 001A-24
	MOV	A,M	;TRY WRITING AND READING IT BACK 001B-7E
	CMA	        ;001C-2F
	MOV	M,A	;001D-77
	CMP	M	;001E-BE
	CMA	        ;001F-2F
	MOV	M,A	;0020-77
	JZ	LOOP	;LOOP TO NEXT PAGE 0021-CA 1A 00 *
	DCX	H	;FOUND THE TOP 0024-2B
	SHLD	MEMTOP	;0025-22 04 00 *
;
;      2. SET UP DEDICATED MEMORY LOCATIONS
;              USER I/O ENTRY POINTS (TOP OF MEMORY)
;              EXIT TEMPLATE
;              USER REGISTERS
;              USER INTERRUPT MASK
;              USER STACK
;              MONITOR STACK
;
	LXI	B,TOS	;0028-01 C8 00 *
	MOV	L,C	;002B-69
	SPHL	        ;SET STACK 002C-F9
SH1:
	LDAX	B	;002D-0A
	MOV	M,A	;002E-77
	INR	C	;002F-0C
	INR	L	;0030-2C
	JNZ	SH1	;0031-C2 2D 00 *
	MVI	L,SLOC	;INIT VALUE FOR USER STACK 0034-2E D1
	MOV	M,H	;0036-74
	DCR	M	;0037-35
;
; TRAP TO MONITOR
;
	MVI	A,(JMP RESTART)	;PUT JMP OPCODE AT INIT 0038-3E C3
	STA	RESET	;FOR NOW, SET TO ZERO 003A-32 00 00 *
	LXI	H,RESTART	;003D-21 37 FF *
	SHLD	RESET+1	;0040-22 01 00 *
;
;  C.  PROGRAM I/O DEVICES
;      1. USART FOR CRT
;      2. USART FOR TTY
;
	MVI	A,ST1+R24@1+CL8 ;04FH
	OUT	CRTC
	MVI	A,ST2+R110+CL8  ;0CEH
	OUT	TTC
	MVI	A,TXEN+DTR+RXEN+RTS ;027H
	OUT	CRTC
	OUT	TTC
;
;      3. HIGH SPEED READER
;      4. HIGH SPEED PUNCH
;      5. TTY READER
;
	MVI	A,0
	OUT	PTRC	;INIT PAPER TAPE READER 0052-D3 F9
;
; STARTING HERE, V1.0 DEPARTS FROM V1.2
;
SH2:
	LXI	H,IOBYT	;GET THE INITIAL IOBYT FROM SHADOW ROM 
	MOV	A,M	;COPY THAT TO RAM UNDERNEATH
	STA	006H	;AT 006H
	ANI	0FCH	;ZERO THE CONSOLE BITS
	MOV	C,A	;SAVE IN C
	LXI	D,VERS	;POINT TO MDS VERSION MESSAGE
	MVI	B,LVER	;PRINT 15H CHARACTERS
	MVI	A,03	;
	ANA	M	;ISOLATE CONSOLE BITS
	CPI	02	;CONSOLE=BATCH?
	JZ	SH7	;IF YES, JUMP TO MONITOR
;	
SH3:
	INR	C	;STEP CONSOLE=TTY->CRT->BATCH->USER
	MOV	M,C	;MAKE THAT THE NEW IOBYT
	MOV	A,C	;COPY TO A
	STA	IOBYT	;REDUNDANT STORE OF IOBYTE
;
; GET CHARACTER
;
	IN	CRTC	;SEE IF CRT USART READY TO RECEIVE
	ANI	02	;ISOLATE RXRDY
	JZ	SH5	;IF NOT, TRY TTY
	IN	CRTI	;GET CRT CHARACTER
	ANI	7FH	;MASK OUT PARITY BIT
	CPI	' '	;USER TYPED SPACEBAR?
	JNZ	SH5	;IF NOT, TRY TTY
	XCHG	        ;NOW HL POINTS TO MESSAGE
SH4:
	MOV	C,M	;GET THE CHARACTER IN THE MESSAGE
	INX	H	;POINT TO NEXT
	CALL	CRTOUT	;PRINT THE VERSION USING MONITOR SUBROUTINE
	DCR	B	;DECREMENT CHARACTER COUNTER
	JNZ	SH4	;PRINT THEM ALL
	JMP	SH7	;DONE WITH MESSAGE, GO TO MONITOR
SH5:
	DCR	C	;
	MOV	A,C
	STA	006H
	MOV	M,C	;UPDATE IOBYT
;
	IN	TTC	;IS TTY USART RCVR READY?
	ANI	02
	JZ	SH3	;IF NOT, LOOP AROUND AGAIN BETWEEN TTY AND CRT
	IN	TTI	;GET TTY CHARACTER
	ANI	7FH	;
	CPI	' '	;SPACEBAR AT TTY?
	JNZ	SH3	;IF NOT, LOOP AROUND AGAIN BETWEEN TTY AND CRT
	XCHG		;NOW HL POINTS TO MESSAGE
;
SH6:
	MOV	C,M	;GET THE CHARACTER
	INX	H	;POINT TO NEXT
	CALL	TTYOUT	;PRINT THE VERSION USING MONITOR SUBROUTINE
	DCR	B	;DECREMENT CHARACTER COUNTER

	JNZ	SH6	;LOOP TO PRINT MORE
SH7:
	JMP	BEGIN	;00A7-C3 00 F8 *
;
; MDS MONITOR SIGN-ON MESSAGE
;
VERS:   DB      CR,LF,'MDS BOOT, V'
        DB      VER/10+'0','.',VER MOD 10+'0'
        DB      CR,LF
LVER    EQU     $-VERS
;
; EXIT CODE TEMPLATE, TO BE EXECUTED IN RAM
; THIS CODE IS ORIGINATED SO AS TO BE ALIGNED
; AGAINST THE TOP OF A PAGE
;
        ORG     SBASE+0C8H
;
USER	EQU	$-8	;@ 0F7C8H      USER= 0F7C0H
TOS:
ELOC:	DB	0EEH	;@ 0F7C8H
DLOC:	DB	0DDH	;@ 0F7C9H
CLOC:	DB	0CCH	;@ 0F7CAH
BLOC:	DB	0BBH	;@ 0F7CBH
	DB	0	;@ 0F7CCH
ILOC:	DB	0FEH	;@ 0F7CDH
FLOC:	DB	0FFH	;CPU FLAGS     ;@ 0F7CEH
ALOC:	DB	0AAH	;@ 0F7CFH
	DB	0C0H	;@ 0F7D0H 
SLOC:	DB	0	;@ 0F7D1H;0BD1-00
;
EXIT:			;@ 0F7D2H
	DI		;MONITOR STACK ORIGIN  0BD2-F3
	POP	D	;0BD3-D1
	POP	B	;0BD4-C1
	POP	PSW	;0BD5-F1
	OUT	0FCH	;OUT SOCP1 0BD6-D3 FC
	POP	PSW	;0BD8-F1
	POP	H	;0BD9-E1
	SPHL		;0BDA-F9
	LXI	H,1234H	;1234H JUST A FILLER,OVERWRITTEN LATER
LLOC	EQU	$-2
HLOC	EQU	$-1
	EI		;0BDE-FB
	JMP	6789H	;6789H JUST A FILLER,OVERWRITTEN LATER
PLOC	EQU	$-1
TLOC:	DW	0	;TRAP 1 ADDRESS 0BE2-00
	DB	0	;TRAP 1 VALUE 0BE3-00
	DW	0	;TRAP 2 ADDRESS
	DB	0	;TRAP 2 VALUE
;
; EXTENSIBLE I/O ENTRY POINTS
;
XTBL:	JMP	0	;00E8-C3 00 00 *
	JMP	0	;00EB-C3 00 00 *
	JMP	0	;00EE-C3 A2 FC *
	JMP	0	;00F1-C3 00 EE *
	JMP	0	;00F4-C3 28 EF *
	JMP	0	;00F7-C3 40 EF *
	JMP	0	;00FA-C3 28 EF *
	JMP	0	;00FD-C3 00 00 *
ENDX:
;
; END OF SHADOW PROM CODE
;
;*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*

	END
