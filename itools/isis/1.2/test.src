;
;
;********************************
;****** 8080 CPU SELF-TEST ******
;********************************
;
	ORG	0
	JMP	58H
	ORG	8H
	EI
	RET
	ORG	10H
	HLT
	ORG	18H
	JMP	LP1
	HLT
	ORG	20H
	HLT
	ORG	28H
	HLT
	ORG	30H
	HLT
	ORG	38H
	HLT
	ORG	40H
LP1:	IN	0F6H
	ANI	04H
	JZ	LP1
	IN	0E6H
	XRA	A
	CMP	B
	JZ	LP2
	HLT		;DI FAILED
LP2:	INR	B
	JMP	INTR
	ORG	58H
	DI
;
;	TEST JMP, JC, JNC, STC, CMC.
;
;
	JMP	START
	HLT
;
	ORG	60H
	TITLE	'8080 CPU SELF-TEST'
START:	JNC	LOO1
	JC	LOO2
	HLT		;JC OR JNC FAILED
LOO1:	JC	LOO3	;JNC WORKS, TRY JC
	STC		;JC DID NOT BRANCH, OK
	JC	LOO4
	HLT		;STC OR JC FAILED
LOO3:	HLT		;JC FAILED
LOO4:	JNC	LOO5	;JC WORKS, TRY JNC
	CMC		;JNC DID NOT BRANCH, OK
	JNC	LOO6
	HLT		;CMC FAILED
LOO5:	HLT		;JNC FAILED
LOO2:	CMC
	JNC	LOO7
	HLT		;CMC OR JNC FAILED
LOO7:	JC	LOO8
	STC		;JC DID NOT BRANCH, OK
	JC	LOO6
	HLT		;STC FAILED
LOO8:	HLT		;JC FAILED
;
;
;TEST THE INSTRUCTIONS JZ, JNZ, XRA A, INR A.
;
;
LOO6:	JNZ	LOO9
	JZ	LO11
	HLT		;JZ OR JNZ FAILED, NONE JUMPED ON SAME CONDITION
			;(Z=1 OR Z=0)
LOO9:	JZ	LO10
	JMP	LO11
LO10:	HLT		;JZ OR JNZ FAILED, BOTH JUMPED ON SAME CONDITION
			;(Z=1 OR Z=0)
LO11:	XRA	A
	JNZ	LO12
	JZ	LO13
	HLT		;JZ FAILED, DID NOT JUMP ON Z=1
LO12:	HLT		;XRA A FAILED
LO13:	INR	A
	JZ	LO14
	JNZ	LO15
	HLT		;JNZ FAILED, DID NOT JUMP ON Z=0
LO14:	HLT		;JZ FAILED, JUMPED ON Z=0, OR INR A FAILED
;
;
;TEST THE INSRUCTIONS MVI, DCR A, INR A.
;
;
LO15:	MVI	A,-1
	INR	A
	JZ	LO16
	HLT		;MVI A,-1 FAILED
LO16:	MVI	A,1
	DCR	A
	JZ	LO17
	HLT		;DCR A FAILED
;
;
;TEST THE INSTRUCTION CMP AND REGISTER A AND REG.B
;
;
LO17:	MVI	B,1
	INR	B
	JNZ	LO18
	HLT		;B-REG. DEFECT
LO18:	DCR	B
	DCR	B
	JZ	LO19
	HLT		;B-REG. DEFECT
LO19:	MVI	A,0
	CMP	B
	JZ	LO20
	HLT		;CMP B FAILED
LO20:	MVI	A,54H
	MVI	B,54H
	INR	A
	INR	B
	CMP	B
	JZ	LO21
	HLT		;A-REG. OR B-REG. DEFECT
LO21:	MVI	A,0ABH
	MVI	B,0ABH
	DCR	A
	DCR	B
	CMP	B
	JZ	LO22
	HLT		;A-REG. OR B-REG. DEFECT
;
;
;TEST THE INSTRUCTIONS JP, JM.
;
;
LO22:	MVI	A,10H
	DCR	A
	JP	LO23
	HLT		;JP FAILED, DID NOT JUMP ON S=0
LO23:	JM	LO25
LO24:	DCR	A
	JZ	LO26
	JP	LO24
	HLT		;JP FAILED, DID NOT JUMP ON S=0
LO25:	HLT		;JM FAILED, JUMPED ON S=0
LO26:	DCR	A
	JM	LO27
	HLT		;JM FAILED, DID NOT JUMP ON S=1
LO27:	JP	LO28
	JM	LO29
LO28:	HLT		;JP FAILED, JUMPED ON S=1
;
;
;TEST THE INSTRUCTIONS JPO, JPE.
;
;
LO29:	XRA	A
	JPO	LO30
	JPE	LO31
	HLT		;JPE FAILED, DID NOT JUMP ON P=1
LO30:	HLT		;JPO FAILED, JUMPED ON P=1
LO31:	INR	A
	JPE	LO32
	JPO	LO33
	HLT		;JPO FAILED, DID NOT JUMP ON P=0
LO32:	HLT		;JPE FAILED, JUMPED ON P=0
;
;
;TEST THE INSTRUCTIONS ADD REG. SUB REG., ADI, SUI.
;
;
LO33:	MVI	A,55H
	MVI	B,0AAH
	ADD	B
	MVI	B,0FFH
	CMP	B
	JZ	LO34
	HLT		;ADD B FAILED
LO34:	SUB	B
	JZ	LO35
	HLT		;SUB B FAILED
LO35:	ADI	0FH
	MVI	B,0FH
	CMP	B
	JZ	LO36
	HLT		;ADI FAILED
LO36:	SUI	5H
	ADI	0A0H
	MVI	B,0AAH
	CMP	B
	JZ	LO37
	HLT		;SUI FAILED
;
;
;TEST THE INSTRUCTIONS RLC, RRC, RAL, RAR, STC, CMC.
;
;
LO37:	MVI	A,0B2H
	STC
	CMC
	JNC	LO38
	HLT		;STC OR CMC FAILED
LO38:	RLC
	JC	LO39
	HLT		;RLC FAILED
LO39:	RRC
	JC	LO40
	HLT		;RRC FAILED
LO40:	RRC
	JNC	LO41
	HLT		;RRC FAILED
LO41:	RAR
	JC	LO42
	HLT		;RAR FAILED
LO42:	RAL
	JNC	LO43
	HLT		;RAL FAILED
LO43:	RLC
	SUI	0B2H
	JZ	LO44
	HLT		;RLC FAILED
LO44:	ADI	0AAH
	RRC
	SUI	055H
	JZ	LO45
	HLT		;RRC FAILED
LO45:	ADI	33H
	RAL
	RAL
	SUI	0CCH
	JZ	LO46
	HLT		;RAL FAILED
LO46:	ADI	0CCH
	RAR
	RAR
	SUI	33H
	JZ	LO47
	HLT		;RAR FAILED
;
;
;TEST THE INSTRUCTIONS ANA, XRA, ORA, REGISTER.
;
;
LO47:	MVI	A,55H
	MVI	B,0AAH
	ANA	B
	JZ	LO48
	HLT		;ANA FAILED
LO48:	MVI	A,33H
	MVI	B,0FH
	ANA	B
	SUI	03H
	JZ	LO49
	HLT		;ANA	FAILED
LO49:	MVI	A,33H
	MVI	B,99H
	XRA	B
	SUI	0AAH
	JZ	LO50
	HLT		;XRA B FAILED
LO50:	MVI	A,55H
	MVI	B,0AAH
	ORA	B
	SUI	0FFH
	JZ	LO51
	HLT		;ORA B FAILED
;
;
;TEST THE INSRUCTIONS ADC REG. SBB REG.
;
;
LO51:	MVI	A,05H
	MVI	B,09H
	STC
	ADC	B
	SUI	0FH
	JZ	LO52
	HLT		;ADC B FAILED
LO52:	MVI	A,0AH
	STC
	SBB	B
	JZ	LO53
	HLT		;SBB B FAILED
LO53:	MVI	A,13H
	MVI	B,22H
	STC
	CMC
	ADC	B
	MVI	B,35H
	SUB	B
	JZ	LO54
	HLT		;ADC B FAILED
LO54:	ADC	B
	MVI	B,35H
	SBB	B
	JZ	LO55
	HLT		;SBB FAILED
;
;
;TEST THE INSTRUCTIONS ACI, SBI.
;
;
LO55:	MVI	A,53H
	STC
	SBI	44H
	STC
	CMC
	SBI	0EH
	JZ	LO56
	HLT		;SBI FAILED
LO56:	ADI	23H
	STC
	ACI	24H
	STC
	CMC
	ACI	07H
	SUI	4FH
	JZ	LO57
	HLT		;ACI FAIED
;
;
;TEST THE INSTRUCTIONS ANI, XRI, ORI.
;
;
LO57:	MVI	A,53H
	ANI	0A3H
	SUI	03H
	JZ	LO58
	HLT		;ANI FAILED
LO58:	MVI	A,53H
	XRI	65H
	SUI	36H
	JZ	LO59
	HLT		;XRI FAILED
LO59:	XRI	0FH
	ANI	0FDH
	ORI	35H
	SUI	3DH
	JZ	LO60
	HLT		;ORI FAILED
LO60:	STC
	ANI	01
	JNC	LO61
	HLT		;ANI FAILED, DID NOT CLEAR CARRY
LO61:	STC
	XRI	02
	JNC	LO62
	HLT		;XRI FAILED, DID NOT CLEAR CARRY
LO62:	STC
	ORI	04
	JNC	LO63
	HLT		;ORI FAILED, DID NOT CLEAR CARRY
;
;
;TEST THE INSRUCTIONS CPI, CMA.
;
;
LO63:	MVI	A,0FH
	CPI	0FH
	JZ	LO64
	HLT		;CPI FAILED
LO64:	CPI	10H
	JC	LO65
	HLT		;CPI FAILED
LO65:	CPI	0EH
	JNC	LO66
	HLT		;CPI FAILED
LO66:	JNZ	LO67
	HLT		;CPI FAILED
LO67:	MVI	A,55H
	CMA
	CPI	0AAH
	JZ	LO68
	HLT		;CMA FAILED
LO68:	MVI	A,0AAH
	CMA
	XRI	55H
	JZ	LO69
	HLT		;CMA FAILED
;
;
;TEST THE INSTRUCTION DAA.
;
;
LO69:	XRA	A	;CLEAR CY AND AC
	MVI	A,9H
	DAA
	XRI	09H
	JZ	LO70
	HLT		;DAA FAILED
LO70:	MVI	A,0BH
	DAA
	XRI	11H
	JZ	LO71
	HLT		;DAA FAILED
LO71:	XRA	A
	MVI	A,0B1H
	DAA
	JNC	LO72
	XRI	11H
	JZ	LO73
LO72:	HLT		;DAA FAILED
LO73:	XRA	A
	STC
	DAA
	XRI	60H
	JZ	LO74
	HLT		;DAA FAILED
;
;
;TEST THE INSTRUCTION MOV(REG.-REG.) AND THE REGISTERS H&L
;
;
LO74:	MVI	A,0FH
	MVI	B,0
	MOV	B,A
	XRA	A
	MOV	A,B
	SUI	0FH
	JZ	LO75
	HLT		;MOV FAILED
LO75:	MVI	H,3CH
	XRA	A
	MOV	A,H
	CPI	3CH
	JZ	LO76
	HLT		;H-REG. DEFECT
LO76:	MVI	L,3CH
	XRA	A
	MOV	A,L
	CPI	3CH
	JZ	LO77
	HLT		;L-REG. DEFECT
LO77:	MVI	H,0C3H
	MVI	L,0C3H
	XRA	A
	MOV	A,H
	CPI	0C3H
	JZ	LO78
	HLT		;H-REG. DEFECT
LO78:	XRA	A
	MOV	A,L
	CPI	0C3H
	JZ	LO79
	HLT		;L-REG. DEFECT
;
;
;TEST THE INSTRUCTIONS STA, LDA, DB, LXI H,55AAH
;
;
LO79:	MVI	A,55H
	STA	2000H	;STORE ACC. IN 2000H
	XRA	A	;ZERO ACC.
	LDA	2000H
	CPI	55H
	JZ	LO80
	HLT		;STA OR LDA FAILED
LO80:	LXI	H,55AAH
	MOV	A,H
	CPI	55H
	JZ	LO81
	HLT		;LXI H,55AAH FAILED
LO81:	MOV	A,L
	CPI	0AAH
	JZ	LO82
	HLT		;LXI H,55AAH FAILED
;
;
;TEST THE INSTRUCTIONS MOV A,M  MOV M,A  INX H
;
;
LO82:	MVI	A,0FH
	STA	2000H	;STORE ACC. IN 2000H
	XRA	A	;ZERO ACC.
	LXI	H,2000H
	MOV	A,M	;LOAD ACC. FROM 2000H
	CPI	0FH
	JZ	LO83
	HLT		;MOV A,M FAILED
LO83:	MVI	A,55H
	MOV	M,A	;STORE ACC. IN 2000H
	XRA	A	;ZERO ACC.
	LDA	2000H
	CPI	55H
	JZ	LO84
	HLT		;MOV M,A FAILED
LO84:	LXI	H,07FEH
	INX	H
	INX	H
	INX	H
	MOV	A,H
	CPI	08H
	JZ	LO85
	HLT		;INX H FAILED
LO85:	MOV	A,L
	CPI	01
	JZ	LO86
	HLT		;INX H FAILED
;
;
;TEST THE INSTRUCTIONS IN AND OUT.
;
;
LO86:	LXI	H,TXT1
LO87:	MOV	A,M	;MOVE LETTER TO ACC.
	CPI	03H	;TEST IF ETX
	JZ	LO89
	STA	2000H	;STORE LETTER
LO88:	IN	0F5H	;INPUT STATUS
	ANI	01H	;TEST IF TTY READY
	JZ	LO88
	LDA	2000H	;LOAD LETTER TO ACC.
	OUT	0F4H	;OUTPUT LETTER
	INX	H
	JMP	LO87
LO89:	IN	0F5H
	ANI	02H
	JZ	LO89
	IN	0F4H
	ANI	7FH	;REMOVE	PARITY BIT
	CPI	43H
	JZ	LO90
	HLT		;IN FAILED
;
;
;DEFINE	STACK-PIONTER
;
;
LO90:	LXI	SP,2700H
	PUSH	B
;
;
;TEST THE INSTRUCTIONS CALL, CC, CNC, RET.
;
;
	LXI	H,55AAH
	XTHL
	LXI	H,5A5AH
	XTHL
	MOV	A,H
	CPI	55H
	JZ	LO91
	HLT		;XTHL FAILED
LO91:	MOV	A,L
	CPI	0AAH
	JZ	LO92
	HLT		;XTHL FAILED
LO92:	POP	B
	CALL	SBR1	;SUBROUTINE INCREMENTS (SP)
	HLT		;CALL FAILED
	STC
	CMC
	CC	SBR2	;TRY CC WHEN C=0
	STC
	CC	SBR1
	HLT		;CC FAILED, DID NOT BRANCH WHEN C=1
	STC
	CNC	SBR3	;TRY CNC WHEN C=1
	CMC		;SET C=0
	CNC	SBR1	;(SP)+1 -> (SP)
	HLT		;CNC FAILED
;
;
;TEST THE INSTRUCTIONS CZ, CNZ, CPE, CPO.
;
;
	XRA	A
	CNZ	SBR4	;TRY CNZ WHEN Z=1
	CZ	SBR1
	HLT		;CZ FAILED
	ADI	05H	;SET Z=0
	CZ	SBR5	;TRY CZ WHEN Z=0
	CNZ	SBR1
	HLT		;CNZ	FAILED
	XRA	A	;CLEAR FLAGS
	CPO	SBR6
	CPE	SBR1
	HLT		;CPE FAILED
	INR	A
	CPE	SBR7
	CPO	SBR1
	HLT		;CPO FAILED
;
;
;TEST CM, CP, RC, RNC, RZ, RNZ, RP, RM, RPO, RPE.
;
;
	XRA	A
	DCR	A
	CP	SBR8
	CM	SBR1
	HLT		;CM FAILED
	XRA	A
	INR	A
	CM	SBR9
	CP	SBR1
	HLT		;CP FAILED
	CALL	SBR10
	HLT		;RNC FAILED
	CALL	SBR11
	HLT		;RC FAILED
	CALL	SBR12
	HLT		;RNZ FAILED
	CALL	SBR13
	HLT		;RZ FAILED
	CALL	SBR14
	HLT		;RPO FAILED
	CALL	SBR15
	HLT		;RPE FAILED
	CALL	SBR16
	HLT		;RP FAILED
	CALL	SBR17
	HLT		;RM FAILED
;
;
;TEST THE REGISTERS C, D, E.
;
;
	MVI	C,7EH
	INR	C
	INR	C
	MOV	A,C
	CPI	80H
	JZ	LO93
	HLT		;C-REG. DEFECT
LO93:	MVI	D,7EH
	INR	D
	INR	D
	MOV	A,D
	CPI	80H
	JZ	LO94
	HLT		;D-REG.	DEFECT
LO94:	MVI	E,7EH
	INR	E
	INR	E
	MOV	A,E
	CPI	80H
	JZ	LO95
	HLT		;E-REG. DEFECT
;
;
;TEST THE INSTRUCTIONS MVI M, INR M, DCR M.
;
;
LO95:	LXI	H,2002H
	MVI	M,55H
	XRA	A
	MOV	A,M
	CPI	55H
	JZ	LO96
	HLT		;MVI M FAILED
LO96:	MVI	M,7EH
	INR	M
	INR	M
	MOV	A,M
	CPI	80H
	JZ	LO97
	HLT		;INR M FAILED
LO97:	MVI	M,41H
	DCR	M
	DCR	M
	MOV	A,M
	CPI	3FH
	JZ	LO98
	HLT		;DCR M FAILED
;
;
;TEST THE INSTRUCTIONS ADD M, SUB M, ADC M, SBB M.
;
;
	ORG	4000H
LO98:	LXI	H,2002H
	MVI	M,3CH
	MVI	A,47H
	ADD	M
	CPI	83H
	JZ	LO99
	HLT		;ADD M FAILED
LO99:	MVI	M,17H
	MVI	A,48H
	SUB	M
	CPI	31H
	JZ	L100
	HLT		;SUB M FAILED
L100:	MVI	M,02H
	MVI	A,02H
	STC
	ADC	M
	ADC	M
	CPI	07H
	JZ	L101
	HLT		;ADC M FAILED
L101:	MVI	M,02H
	MVI	A,07H
	STC
	SBB	M
	SBB	M
	CPI	02H
	JZ	L102
	HLT		;SBB M FAILED
;
;
;TEST THE INSTRUCTIONS LXI B, LXI D.
;
;
L102:	LXI	B,55AAH
	XRA	A
	MOV	A,B
	CPI	55H
	JZ	L103
	HLT		;LXI B FAILED
L103:	MOV	A,C
	CPI	0AAH
	JZ	L104
	HLT		;LXI B FAILED
L104:	LXI	D,5AA5H
	XRA	A
	MOV	A,D
	CPI	5AH
	JZ	L105
	HLT		;LXI D FAILED
L105:	MOV	A,E
	CPI	0A5H
	JZ	L106
	HLT		;LXI D FAILED
;
;
;TEST THE INSTRUCTIONS PUSH B, POP B, POP D.
;
;
L106:	LXI	B,6996H
	PUSH	B
	LXI	B,0000H
	POP	B
	MOV	A,B
	CPI	69H
	JZ	L107
	HLT		;PUSH B OR POP B FAILED
L107:	MOV	A,C
	CPI	96H
	JZ	L108
	HLT		;PUSH B OR POP B FAILED
L108:	LXI	B,0C33CH
	PUSH	B
	LXI	D,0000H
	POP	D
	MOV	A,D
	CPI	0C3H
	JZ	L109
	HLT		;POP D FAILED
L109:	MOV	A,E
	CPI	3CH
	JZ	L110
	HLT		;POP D FAILED
;
;
;TEST THE INSTRUCTIONS PUSH D, PUSH H.
;
;
L110:	LXI	D,0A55AH
	PUSH	D
	LXI	D,0000H
	POP	D
	MOV	A,D
	CPI	0A5H
	JZ	L111
	HLT		;PUSH D FAILED
L111:	MOV	A,E
	CPI	5AH
	JZ	L112
	HLT		;PUSH D FAILED
L112:	LXI	H,55AAH
	PUSH	H
	LXI	H,0000H
	POP	D
	MOV	A,D
	CPI	55H
	JZ	L113
	HLT		;PUSH H FAILED
L113:	MOV	A,E
	CPI	0AAH
	JZ	L114
	HLT		;PUSH H FAILED
;
;
;TEST THE INSTRUCTIONS POP H, POP PSW, PUSH PSW.
;
;
L114:	LXI	H,0AA55H
	PUSH	H
	LXI	H,0000H
	POP	H
	MOV	A,H
	CPI	0AAH
	JZ	L115
	HLT		;POP H FAILED
L115:	MOV	A,L
	CPI	55H
	JZ	L116
	HLT		;POP H FAILED
L116:	LXI	B,5502H
	PUSH	B
	XRA	A
	POP	PSW
	JM	L117
	JZ	L117
	JPE	L117
	JC	L117
	JNC	L118
L117:	HLT		;POP PSW FAILED
L118:	CPI	55H
	JZ	L119
	HLT		;POP PSW FAILED
L119:	LXI	B,0AAC7H
	PUSH	B
	XRA	A
	POP	PSW
	JP	L120
	JNZ	L120
	JPO	L120
	JNC	L120
	JC	L121
L120:	HLT		;POP PSW FAILED
L121:	XRA	A
	PUSH	PSW
	ADI	01H
	POP	PSW
	JNZ	L122
	JPO	L122
	JNC	L123
L122:	HLT		;PUSH PSW FAILED
;
;
;TEST THE INSTRUCTIONS DAD B, INX B.
;
;
L123:	LXI	H,366BH
	LXI	B,11EAH
	DAD	B
	MOV	A,H
	CPI	48H
	JZ	L124
	HLT		;DAD B FAILED
L124:	MOV	A,L
	CPI	55H
	JZ	L125
	HLT		;DAD B FAILED
L125:	LXI	B,3FFDH
	INX	B
	INX	B
	INX	B
	MOV	A,B
	CPI	40H
	JZ	L126
	HLT		;INX B FAILED
L126:	MOV	A,C
	CPI	00H
	JZ	L127
	HLT		;INX B FAILED
;
;
;TEST THE INSTRUCTIONS DAD D, INX D
;
;
L127:	LXI	H,366BH
	LXI	D,11EAH
	DAD	D
	MOV	A,H
	CPI	48H
	JZ	L128
	HLT		;DAD D FAILED
L128:	MOV	A,L
	CPI	55H
	JZ	L129
	HLT		;DAD D FAILED
L129:	LXI	D,3FFEH
	INX	D
	INX	D
	INX	D
	MOV	A,D
	CPI	40H
	JZ	L130
	HLT		;INX D FAILED
L130:	MOV	A,E
	CPI	01H
	JZ	L131
	HLT		;INX D FAILED
;
;
;TEST THE INSTRUCTIONS DAD H, STAXB, LDAXB.
;
;
L131:	LXI	H,5555H
	DAD	H
	XRA	A
	MOV	A,H
	CPI	0AAH
	JZ	L132
	HLT		;DAD H FAILED
L132:	XRA	A
	MOV	A,L
	CPI	0AAH
	JZ	L133
	HLT		;DAD H FAILED
L133:	LXI	B,2002H
	MVI	A,36H
	STAX	B
	XRA	A
	LDA	2002H
	CPI	36H
	JZ	L134
	HLT		;STAXB FAILED
L134:	XRA	A
	LDAX	B
	CPI	36H
	JZ	L135
	HLT		;LDAXB FAILED
;
;
;TEST THE INSTRUCTIONS XCHG, LDAX D, STAX D.
;
;
L135:	LXI	H,3355H
	LXI	D,6767H
	XCHG
	XRA	A
	MOV	A,H
	CPI	67H
	JZ	L136
	HLT		;XCHG FAILED
L136:	MOV	A,L
	SUB	E
	ADD	D
	CPI	45H
	JZ	L137
	HLT		;XCHG FAILED
L137:	LXI	D,2002H
	MVI	A,63H
	STA	2002H
	XRA	A
	LDAX	D
	CPI	63H
	JZ	L138
	HLT		;LDAX D FAILED
L138:	MVI	A,55H
	STAX	D
	XRA	A
	LDAX	D
	CPI	55H
	JZ	L139
	HLT		;STAX D FAILED
;
;
;TEST THE INSTRUCTIONS DCX B, DCX D.
;
;
L139:	LXI	B,4001H
	DCX	B
	DCX	B
	MVI	A,3FH
	CMP	B
	JZ	L140
	HLT		;DCX B FAILED
L140:	MVI	A,0FFH
	CMP	C
	JZ	L141
	HLT		;DCX B FAILED
L141:	LXI	D,4001H
	DCX	D
	DCX	D
	MVI	A,3FH
	CMP	D
	JZ	L142
	HLT		;DCX D FAILED
L142:	MVI	A,0FFH
	CMP	E
	JZ	L143
	HLT		;DCX D FAILED
;
;
;TEST THE INSTRUCTIONS DCX H, NOP.
;
;
L143:	LXI	H,4001H
	DCX	H
	DCX	H
	MVI	A,3FH
	CMP	H
	JZ	L144
	HLT		;DCX H FAILED
L144:	MVI	A,0FFH
	CMP	L
	JZ	L145
	HLT		;DCX H FAILED
L145:	XRA	A
	NOP
	JZ	L146
	HLT		;NOP FAILED
L146:	XRA	A
	STC
	NOP
	JC	L147
	HLT		;NOP FAILED
;
;
;TEST THE INSTRUCTIONS SHLD, LHLD.
;
;
L147:	LXI	H,3553H
	SHLD	2010H
	LDA	2010H
	CPI	53H
	JZ	L148
	HLT		;SHLD FAILED
L148:	LDA	2011H
	CPI	35H
	JZ	L149
	HLT		;SHLD FAILED
L149:	LXI	H,5533H
	SHLD	2010H
	LXI	H,0000H
	LHLD	2010H
	MOV	A,H
	CPI	55H
	JZ	L150
	HLT		;LHLD FAILED
L150:	MOV	A,L
	CPI	33H
	JZ	L151
	HLT		;LHLD FAILED
;
;
;TEST THE INSTRUCTIONS LXI SP, SPHL.
;
;
L151:	LXI	SP,210BH
	MVI	A,77H
	PUSH	PSW
	XRA	A
	LDA	210AH
	CPI	77H
	JZ	L152
	HLT		;LXI SP FAILED
L152:	POP	PSW
	LXI	B,0FF0H
	PUSH	B
	LXI	H,2109H
	LXI	SP,0000H
	SPHL
	POP	B
	MOV	A,B
	CPI	0FH
	JZ	L153
	HLT		;SPHL FAILED
L153:	MOV	A,C
	CPI	0F0H
	JZ	L154
	HLT		;SPHL FAILED
;
;
;TEST THE INSTRUCTIONS DCX SP, INX SP.
;
;
L154:	LXI	B,3663H
	LXI	D,0550H
	PUSH	B
	PUSH	D
	INX	SP
	INX	SP
	POP	B
	MOV	A,B
	CPI	36H
	JZ	L155
	HLT		;INX SP FAILED
L155:	MOV	A,C
	CPI	63H
	JZ	L156
	HLT		;INX SP FAILED
L156:	DCX	SP
	DCX	SP
	DCX	SP
	DCX	SP
	POP	D
	MOV	A,D
	CPI	05H
	JZ	L157
	HLT		;DCX SP FAILED
L157:	MOV	A,E
	CPI	50H
	JZ	L158
	HLT		;DCX SP FAILED
	HLT		;INX SP FAILED
;
;
;TEST THE INSTRUCTIONS DAD SP, PCHL.
;
;
L158:	POP	B
	LXI	H,0000H
	LXI	B,3355H
	PUSH	B
	DAD	SP
	SPHL
	POP	B
	MOV	A,B
	CPI	33H
	JZ	L159
	HLT		;DAD SP FAILED
L159:	MOV	A,C
	CPI	55H
	JZ	L160
	HLT		;DAD SP FAILED
L160:	LXI	H,L161
	PCHL
	NOP
	NOP
	HLT		;PCHL FAILED
;
;
;TEST THE INSTRUCTIONS ANA M, XRA M, ORA M, CMP M.
;
;
L161:	LXI	H,2002H
	MVI	M,0FH
	MVI	A,55H
	ANA	M
	CPI	05H
	JZ	L162
	HLT		;ANA M FAILED
L162:	XRA	M
	CPI	0AH
	JZ	L163
	HLT		;XRA M FAILED
L163:	ORA	M
	CPI	0FH
	JZ	L164
	HLT		;ORA M FAILED
L164:	LXI	H,2002H
	MVI	M,36H
	MVI	A,36H
	CMP	M
	JZ	L165
	HLT		;CMP M FAILED
L165:	INR	M
	INR	A
	CMP	M
	JZ	L166
	HLT		;CMP M FAILED
;
;
;TEST THE INSTRUCTIONS EI, DI.
;
;
L166:	MVI	B,00H
	LXI	H,TXT2	;TYPE A LETTER
	CALL	TXTUT
	EI
L167:	IN	0F6H	;IN STATUS
	ANI	04H
	JZ	L167
	HLT		;EI FAILED
INTR:	DI
	LXI	H,TXT4
	CALL	TXTUT	;TYPE A LETTER
L168:	IN	0F6H	;IN STATUS
	ANI	04H
	JZ	L168
	IN	0E6H
	LXI	H,TXT3
	CALL	TXTUT
	JMP	4400H	;JUMP TO CPU-RAM TEST
;
;*************************
;****** SUBROUTINES ******
;*************************
;
;(SP)+1 ->(SP)
;
SBR1:	XTHL
	INX	H
	XTHL
	RET
	HLT		;RET FAILED
;
;
;HLT
;
;
SBR2:	HLT		;CC FAILED
SBR3:	HLT		;CNC FAILED
SBR4:	HLT		;CNZ FAILED
SBR5:	HLT		;CZ FAILED
SBR6:	HLT		;CPO FAILED
SBR7:	HLT		;CPE FAILED
SBR8:	HLT		;CP FAILED
SBR9:	HLT		;CM FAILED
SBR10:	STC
	RNC
	XTHL
	INX	H
	XTHL
	RC
	HLT		;RC FAILED
SBR11:	STC
	CMC
	RC
	XTHL
	INX	H
	XTHL
	RNC
	HLT		;RNC FAILED
SBR12:	XRA	A
	RNZ
	XTHL
	INX	H
	XTHL
	RZ
	HLT		;RZ FAILED
SBR13:	XRA	A
	ADI	05H
	RZ
	XTHL
	INX	H
	XTHL
	RNZ
	HLT		;RNZ FAILED
SBR14:	XRA	A
	RPO
	XTHL
	INX	H
	XTHL
	RPE
	HLT		;RPE FAILED
SBR15:	XRA	A
	INR	A
	RPE
	XTHL
	INX	H
	XTHL
	RPO
	HLT		;RPO FAILED
SBR16:	XRA	A
	DCR	A
	RP
	XTHL
	INX	H
	XTHL
	RM
	HLT		;RM FAILED
SBR17:	XRA	A
	INR	A
	RM
	XTHL
	INX	H
	XTHL
	RP
	HLT		;RP FAILED
TXTUT:	MOV	A,M
	CPI	03H
	RZ
	CALL	OUTTY
	INX	H
	JMP	TXTUT
OUTTY:	STA	2000H
INSTA:	IN	0F5H	;INPUT STATUS
	ANI	01H	;TEST IF TTY READY
	JZ	INSTA
	LDA	2000H
	OUT	0F4H
	RET
INN:	IN	0F5H
	ANI	02H
	JZ	INN
	ANI	7FH	;REMOVE PARITY BIT
	RET
;
;**************************************
;****** TEXT AND REGISTER DEFINE ******
;**************************************
;
TXT1:	DB	0DH,0AH,'TEXT OUT WORKS, TYPE "C"!',0DH,0AH,03H
TXT3:	DB	0DH,0AH,'END CPU-TEST',0DH,0AH
	DB	0DH,0AH,'START CPU-RAM-TEST',0DH,0AH,03H
TXT2:	DB	0DH,0AH,'TYPE A LETTER ON TDV 2100!',0DH,0AH,03H
TXT4:	DB	0DH,0AH,'TYPE ANOTHER LETTER ON TDV 2100!',0DH,0AH,03H
	END
