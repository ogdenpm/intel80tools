;
;**************************
;****** RAM-CPU TEST ******
;**************************
;
;
;*****************************
;****** CONSTANT DEFINE ******
;*****************************
;
STATUS	EQU	0F5H
DATA	EQU	0F4H
ETX	EQU	03H
CR	EQU	0DH
LF	EQU	0AH
;
;
;
;
	ORG	4400H
	TITLE	'RAM - CPU TEST'
;
;****************************************
;****** TEST THE 8 FIRST ADDRESSES ******
;****************************************
;
	LXI	SP,2700H
;
;LOAD ADDR. 0 TO AH WITH COUNTING
;
	LXI	H,2000H
	MVI	C,00H
T001:	MOV	M,C
	INR	C
	INX	H
	MOV	A,L
	CPI	0AH
	JNZ	T001
;
;READ AND CHECK
;
	LXI	D,2000H
	MVI	H,00H
T002:	LDAX	D
	PUSH	H
	CMP	H
	CNZ	ERR2
	POP	H
	INR	H
	INX	D
	MOV	A,E
	CPI	0AH
	JNZ	T002
;
;LOAD ALTERNATING WITH 55H AND 0AAH
;
	LXI	H,2000H
T003:	MVI	M,55H
	INX	H
	MVI	M,0AAH
	INX	H
	MOV	A,L
	CPI	0AH
	JNZ	T003
;
;
;READ AND CHECK
;
	LXI	D,2000H
T004:	MVI	H,55H
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	MVI	H,0AAH
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	MOV	A,E
	CPI	0AH
	JNZ	T004
;
;LOAD ALTERNATING WITH 0AAH AND 55H
;
	LXI	H,2000H
T005:	MVI	M,0AAH
	INX	H
	MVI	M,55H
	INX	H
	MOV	A,L
	CPI	0AH
	JNZ	T005
;
;READ AND CHECK
;
	LXI	D,2000H
T006:	MVI	H,0AAH
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	MVI	H,55H
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	MOV	A,E
	CPI	0AH
	JNZ	T006
;
;
;*******************************
;****** MAIN CPU-RAM TEST ******
;*******************************
;
	LXI	SP,6000H
;
;ADDRESS TEST
;
	MVI	A,32H	;50 LOOPS
STADT:	STA	2000H
	LXI	H,200AH	;RELATIVE STARTING ADDRESS
	LXI	D,200AH	;ABSOLUTE STARTING ADDRESS
	MVI	B,13H	;CRC POLYNOMIAL
L003:	MVI	C,8H
	PUSH	H	;SAVE RELATIVE ADDRESS
L002:	DAD	H	;SHIFT H LEFT
	JNC	L001	;MOST SIGNIFICANT BIT (BEFORE SHIFT)=0
	MOV	A,H
	XRA	B	;PERFORM MOD 2 ADDITION
	MOV	H,A
	ANI	3FH	;GET 6 LOWER BITS
L001:	DCR	C	;LOOP 8 TIMES
	JNZ	L002
	MOV	A,H
	STAX	D
	INX	D	;INCREMENT ABSOLUTE ADDRESS
	POP	H	;GET RELATIVE ADDRESS
	INX	H
	MOV	A,D
	ANI	0D7H
	ORA	E	;TEST FOR FINISHED
	JNZ	L003	;NOT FINISHED
;
;
;READ AND CHECK
;
;
	LXI	H,200AH
	LXI	D,200AH
	MVI	B,13H
L006:	MVI	C,8H
	PUSH	H
L005:	DAD	H
	JNC	L004
	MOV	A,H
	XRA	B
	MOV	H,A
	ANI	3FH
L004:	DCR	C
	JNZ	L005
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	POP	H
	INX	H
	MOV	A,D
	ANI	0D7H
	ORA	E
	JNZ	L006
	LDA	2000H
	DCR	A	;DECREMENT LOOP-COUNTER
	JNZ	STADT	;REPEAT LOOP IF NOT FINISHED
;
;LOAD RAM WITH COUNTING
;
	MVI	A,32H	;50 LOOPS
SCOT:	STA	2000H	;LOAD LOOP COUNTER
	LXI	H,200AH
	MVI	B,0AH
R001:	MOV	M,B
	INR	B
	INX	H
	MOV	A,H
	CPI	28H
	JNZ	R001
;
;
;READ AND CHECK
;
	LXI	D,200AH
	MVI	H,0AH
R002:	LDAX	D
	PUSH	H
	CMP	H
	CNZ	ERR2
	POP	H
	INR	H
	INX	D
	MOV	A,D
	CPI	28H
	JNZ	R002
	LDA	2000H
	DCR	A	;DECREMENT LOOP COUNTER
	JNZ	SCOT	;REPEAT LOOP IF NOT FINISHED
;
;LOAD RAM ALTERNATING WITH 55H AND 0AAH
;
	MVI	A,32H	;50 LOOPS
SCHT1:	STA	2000H	;LOAD LOOP COUNTER
	LXI	H,200AH
R003:	MVI	M,55H
	INX	H
	MVI	M,0AAH
	INX	H
	MOV	A,H
	CPI	28H
	JNZ	R003
;
;READ AND CHECK
;
	LXI	D,200AH
R004:	MVI	H,55H
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	MVI	H,0AAH
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	MOV	A,D
	CPI	28H
	JNZ	R004
	LDA	2000H
	DCR	A	;DECREMENT LOOP COUNTER
	JNZ	SCHT1	;REPEAT IF NOT FINISHED
;
;LOAD RAM ALTERNATING WITH 0AAH AND 55H
;
	MVI	A,32H	;50 LOOPS
SCHT2:	STA	2000H
	LXI	H,200AH
R005:	MVI	M,0AAH
	INX	H
	MVI	M,55H
	INX	H
	MOV	A,H
	CPI	28H
	JNZ	R005
;
;READ AND CHECK
;
	LXI	D,200AH
R006:	MVI	H,0AAH
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	MVI	H,55H
	LDAX	D
	CMP	H
	CNZ	ERR2
	INX	D
	MOV	A,D
	CPI	28H
	JNZ	R006
	LDA	2000H
	DCR	A
	JNZ	SCHT2
	LXI	H,TXT4	;END CPU-RAM TEST
	CALL	TXTUT
	HLT
;
;*************************
;****** SUBROUTINES ******
;*************************
;
ERR2:	MOV	B,A	;READ DATA TO B-REGISTER
	CALL	DOUT	;OUTPUT READ DATA
	MOV	B,H	;CORRECT DATA TO B-REGISTER
	LXI	H,TXT1
	CALL	TXTUT
	CALL	DOUT	;OUTPUT CORRECT DATA
	LXI	H,TXT2
	CALL	TXTUT
	LXI	H,TXT3
	CALL	TXTUT
	MOV	B,D	;OUTPUT ADDRESS
	CALL	DOUT
	MOV	B,E
	CALL	DOUT
	LXI	H,TXT5	;CONTINUE?
	CALL	TXTUT
INN:	IN	STATUS
	ANI	02H
	JZ	INN
	IN	DATA
	MVI	B,13H
	RET
TXTUT:	MOV	A,M
	CPI	ETX
	RZ
	CALL	OUTTY
	INX	H
	JMP	TXTUT
OUTTY:	STA	2001H
INSTA:	IN	STATUS
	ANI	01H
	JZ	INSTA
	LDA	2001H
	OUT	DATA
	RET
DOUT:	MOV	A,B
	RLC
	ANI	01H
	ADI	30H
	CALL	OUTTY
	MOV	A,B
	RLC
	RLC
	ANI	01H
	ADI	30H
	CALL	OUTTY
	MOV	A,B
	RLC
	RLC
	RLC
	ANI	01H
	ADI	30H
	CALL	OUTTY
	MOV	A,B
	RLC
	RLC
	RLC
	RLC
	ANI	01H
	ADI	30H
	CALL	OUTTY
	MOV	A,B
	RRC
	RRC
	RRC
	ANI	01H
	ADI	30H
	CALL	OUTTY
	MOV	A,B
	RRC
	RRC
	ANI	01H
	ADI	30H
	CALL	OUTTY
	MOV	A,B
	RRC
	ANI	01H
	ADI	30H
	CALL	OUTTY
	MOV	A,B
	ANI	01H
	ADI	30H
	CALL	OUTTY
	RET
;
;*************************
;****** TEXT DEFINE ******
;*************************
;
TXT1:	DB	' WAS READ',CR,LF,ETX
TXT2:	DB	' SHOULD BE READ',CR,LF,ETX
TXT3:	DB	'BINARY ADDRESS:',CR,LF,ETX
TXT4:	DB	'END CPU-RAM TEST',CR,LF,ETX
TXT5:	DB	CR,LF,'CONTINUE? TYPE SPACE-BAR!',CR,LF,LF,ETX
	END
